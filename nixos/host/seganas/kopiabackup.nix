
{ inputs, config, pkgs, nixpkgs, lib, ... }:
let  
  kopiaCacheDir = "/var/kopia/cache";
  kopiaCacheLog = "/var/kopia/log";
  backupServerSshPubKey = pkgs.writeText "kopia.keys" ''
    192.168.0.59 ssh-rsa AAAAB3NzaC1yc2EAAAADAQABAAABgQDG5juRNHfkebF/v7xRhI+F/ZNHuiy57tW390GZvmMi6f/rHdxwPgmt4GkKIrQgRJy+IwemQbGIc8AX41hzUB1rMA0bzDtmNDa90pYr3Vd+2q8on4k6Xfk2kF6v+exrEoHhcTz3UD7E3071nzKxrHlpwTfWdTa2oX1CroLFQyrtQ3I8tFFVx/AABp3VfVuCGJJ0HEwNAj3i0b8qo+1si4fQC8+CL+UOclRs5nz8Y4NYivzLW0D04sb36/032F9YkfxL7qioybigRmGUFqUxKrgeyu5FUPjXtbVe8DNYiuOKNSU1LILuxNRDY1ctHHdauFWPk02+fec7B7A+jkI7DsutMjee2IbXz5He4Gecr41ed/p4qffCTNRa6+B4FD/Ixq6h64raqIoStSqsBCu4ijxCOd01L9GZ1+rO7Hu8PRm6UAjykq44h+bUrnZEhVy5HINFydhWPrLHc8LFUWqrvLiPkCVXlQml+3PB0y/UofCHyeEtERepIoxOZ/07ux8sQOM=
    192.168.0.59 ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIHJJUJlvVkivTDC9dHcZVVluo1j9Jfcm2MhI8fdx7TTC
    192.168.0.59 ecdsa-sha2-nistp256 AAAAE2VjZHNhLXNoYTItbmlzdHAyNTYAAAAIbmlzdHAyNTYAAABBBNddSwvR7wmeBay2UaskdqzvnU8K7jEF1VY54OkL8FScX9C1SGi4yCc+zHrXmuu1jZT+3gt3aMcHzHAz9uewPD4=
    '';

   kopiaRepoConfig = pkgs.writeText "repo.config" ''
    {
      "storage": {
        "type": "sftp",
        "config": {
          "path": "/backup/kopia",
          "host": "192.168.0.59",
          "port": 22,
          "username": "root",
          "keyfile": "${config.sops.secrets."kopia-backup-sshkey".path}",
          "knownHostsFile": "${backupServerSshPubKey}",
          "externalSSH": false,
          "sshCommand": "ssh",
          "dirShards": null
        }
      },
      "caching": {
        "cacheDirectory": "${kopiaCacheDir}",
        "maxCacheSize": 5242880000,
        "maxMetadataCacheSize": 5242880000,
        "maxListCacheDuration": 30
      },
      "hostname": "${config.networking.hostName}",
      "username": "root",
      "description": "Backup Repository",
      "enableActions": false,
      "formatBlobCacheDuration": 900000000000
    }
   '';
    kopia-backup = pkgs.writeShellScriptBin "kopia" ''
    
    export KOPIA_PASSWORD=$(cat ${config.sops.secrets.kopia_passphrase.path})
    export KOPIA_CONFIG_PATH=/tmp/kopia/repo.config
    export KOPIA_CACHE_DIRECTORY=${kopiaCacheDir}
    export KOPIA_LOG_DIR=${kopiaCacheLog}
    export KOPIA_CHECK_FOR_UPDATES=false
    export KOPIA_PERSIST_CREDENTIALS_ON_CONNECT=false

    # Workarrond to avoid https://github.com/kopia/kopia/blob/master/repo/maintenance/maintenance_run.go#L178 due is creating lock file on the directory where config lives that as being generated by nix is read-only 
    # Other option would be to patch kopia to save this lock file somewhere else but i prefer this option as won't require maintainance.
    mkdir -p /tmp/kopia
    cp ${kopiaRepoConfig} ''${KOPIA_CONFIG_PATH}

    exec ${pkgs.kopia}/bin/kopia "$@"
  '';
in
{
  
  environment.systemPackages = [
    kopia-backup
  ];

  sops.secrets."kopia-backup-sshkey" = {
    sopsFile = ../../../secrets/common/kopia-ssh.key;
    format = "binary";
  };

  sops.secrets.kopia_passphrase = {};

  systemd = {
    timers.kopia-backup = {
      enable = true;
      wantedBy = ["timers.target"];
      partOf = ["kopia-backup.service"];
      timerConfig.OnCalendar = "05:00";
    };
    services.kopia-backup = {
      serviceConfig = {
        Type = "oneshot";
      };
      script = ''
        ${kopia-backup}/bin/kopia snapshot create ${lib.concatStringsSep " " config.nas.backup.sourceDirectories}
      '';
    };

    timers.kopia-verify = {
      enable = true;
      wantedBy = ["timers.target"];
      partOf = ["kopia-verify.service"];
      timerConfig.OnCalendar = "*-*-01 00:00:00";
    };
    services.kopia-verify = {
      serviceConfig = {
        Type = "oneshot";
      };
      script = ''
        ${kopia-backup}/bin/kopia snapshot verify --verify-files-percent=1 --file-parallelism=4 --parallel=4
      '';
    };
  };
}